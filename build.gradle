plugins {
    id 'java'
    id 'com.bmuschko.docker-remote-api' version '6.4.0'
}

import com.bmuschko.gradle.docker.tasks.image.Dockerfile
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

archivesBaseName = 'amazon-chime-voiceconnector-recordandtranscribe'
repositories {
    mavenCentral()
}

dependencies {
    compile group: 'io.reactivex.rxjava2', name: 'rxjava', version: '2.2.2'
    compile(
            'software.amazon.awssdk:transcribestreaming:2.2.0',

            'com.amazonaws:aws-java-sdk-dynamodb:1.11.475',
            'com.amazonaws:aws-java-sdk-kinesisvideo:1.11.475',
            'com.amazonaws:aws-lambda-java-core:1.2.0',
            'com.amazonaws:aws-lambda-java-events:2.2.6',
            'com.amazonaws:aws-java-sdk-cloudwatch:1.11.592',
            'com.amazonaws:amazon-kinesis-video-streams-parser-library:1.0.13',
            'org.slf4j:slf4j-api:1.7.24',
            'org.slf4j:slf4j-log4j12:1.7.24',

            // need this for our async clients
            'software.amazon.awssdk:netty-nio-client:2.2.0',

            // need this for logging
            'org.apache.commons:commons-lang3:3.6',

            // need for argument parsing
            'commons-cli:commons-cli:1.4'
    )
}

task dockerJar(type: Jar) {
    manifest {
        attributes 'Main-Classs': 'com.amazonaws.kvstranscribestreaming.KVSTranscribeStreamingDocker'
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

task copyJarToDocker(type: Copy) {
    from dockerJar
    into "$buildDir/docker"
}

task buildDockerFile(type: Dockerfile) {
    from("alpine:latest")
    runCommand("apk --no-cache add openjdk8 openjdk8-jre")
    workingDir("/tmp/chime-streaming-transcribe/")
    addFile(archivesBaseName + ".jar", "./lib/")
}

task buildDockerImage(type: DockerBuildImage) {
    dependsOn(copyJarToDocker, buildDockerFile)
    images.add('chime-transcribe:latest')
}

task buildZip(type: Zip) {
    from compileJava
    from processResources

    into('lib') {
        from configurations.compileClasspath
    }
}

task copyCFTemplate(type: Copy){
    from "infrastructure/deployment-template.json"
    into "$buildDir/distributions"
}

task zipAndCopyRunTaskFunction(type: Zip){
    from "infrastructure/sendRunTaskRequestLambdaFunction.js"
    archiveFileName = "send-run-task-request-function.zip"
    destinationDirectory = file("$buildDir/distributions")
}

build.dependsOn buildZip, copyCFTemplate, zipAndCopyRunTaskFunction, buildDockerFile